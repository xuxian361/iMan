apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    signingConfigs {
        debug {
            storeFile project.rootProject.file('iMan.jks')
            storePassword KEYSTORE_PASSWORD
            keyAlias 'iMan'
            keyPassword KEY_PASSWORD
        }
        release {
            storeFile project.rootProject.file('iMan.jks')
            storePassword KEYSTORE_PASSWORD
            keyAlias 'iMan'
            keyPassword KEY_PASSWORD
        }
    }

    // 移除lint检查的error
    lintOptions {
        abortOnError false
        warning 'InvalidPackage'

    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    defaultConfig {
        multiDexEnabled = true
        applicationId "com.sundy.iman"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            //设置支持的SO库架构（开发者可以根据需要，选择一个或多个平台的so）
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
        manifestPlaceholders = [
                EASEMOB_APPKEY: "1185170930178964#iman",
                GAO_DE_MAP_KEY: "285db644f8bc3cc08439e4a2907816b2"
        ]

        android.applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def fileName = "iMan_${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "SERVER_URL", "\"http://39.108.238.138/api/\""
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "String", "SERVER_URL", "\"http://39.108.238.138/iman/api/\""
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'src/main/java-gen']
        }
    }

    productFlavors {
        //测试：59cb048daed1795e4300005a
        //发布：59cb1990ae1bf824b6000021
        google {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "google", UMENG_APPKEY_VALUE: "59cb048daed1795e4300005a"]
        }
        baidu {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "baidu", UMENG_APPKEY_VALUE: "59cb048daed1795e4300005a"]
        }
        tencent {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "tencent", UMENG_APPKEY_VALUE: "59cb048daed1795e4300005a"]
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '25.2.0'
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    //处理方法数大于65K
    compile 'com.android.support:multidex:1.0.1'
    //网络请求
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    //消息传递
    compile 'org.greenrobot:eventbus:3.0.0'
    //依赖注入
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //Paper DB 数据存储
    compile 'io.paperdb:paperdb:2.6'
    //日志打印
    compile 'com.orhanobut:logger:2.1.1'
    //Flow Layout控件
    compile 'com.hyman:flowlayout-lib:1.1.2'
    //RecycleView & Adapter
    compile 'com.android.support:recyclerview-v7:25.2.0'
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'
    //SwipeLayout
    compile 'com.daimajia.swipelayout:library:1.2.0@aar'
    //图片显示
    compile 'com.ycjiang:imgepreviewlibrary:1.1.2'
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'com.nineoldandroids:library:2.4.0'
    //Tab Layout
    compile 'com.flyco.tablayout:FlycoTabLayout_Lib:2.1.2@aar'
    //二维码扫描
    compile 'com.journeyapps:zxing-android-embedded:3.5.0'
    //高德定位
    compile 'com.amap.api:location:latest.integration'
    compile 'com.amap.api:3dmap:latest.integration'
    compile 'com.amap.api:search:latest.integration'
    //图片裁剪
    compile 'com.github.yalantis:ucrop:2.2.1'
    //简化实体类
    compile 'org.projectlombok:lombok:1.16.18'
    //图片压缩
    compile 'top.zibin:Luban:1.1.3'
    //圆形图片工具
    compile 'de.hdodenhof:circleimageview:2.2.0'
    //引导介绍页
    compile 'cn.bingoogolapple:bga-banner:2.1.7@aar'
    //权限管理
    compile 'com.yanzhenjie:permission:1.1.0'
    //友盟统计
    compile 'com.umeng.analytics:analytics:latest.integration'
    //七牛云
    compile 'com.qiniu:qiniu-android-sdk:7.3.+'
    //下拉刷新
    compile 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.3'
    //Sticky RecycleView：粘性头部
    compile 'com.timehop.stickyheadersrecyclerview:library:0.4.3@aar'
    //数字进度条
    compile 'com.daimajia.numberprogressbar:library:1.4@aar'
    compile project(':libraries:mediapicker')
}
